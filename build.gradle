buildscript
{
    repositories
    {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }

    dependencies
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

if (project.hasProperty("dev"))
{
    version += "-dev" + dev
}

group = "mchorse.chameleon"
archivesBaseName = "chameleon"

sourceCompatibility = targetCompatibility = '1.8'

compileJava
{
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft
{
    version = "1.12.2-14.23.5.2799"
    runDir = "run"
    mappings = "snapshot_20171003"

    replace("%VERSION%", project.version)
    replace("%MCLIB%", project.mclib)
    replace("%METAMORPH%", project.metamorph)
    replace("%GECKOLIB%", project.geckolib)
}

dependencies
{
    compile files("run/libs/mclib-${mclib}-${project.minecraft.version}-dev.jar")
    compile files("run/libs/metamorph-${metamorph}-${project.minecraft.version}-dev.jar")
    compile files("run/libs/forge-1.12.2-geckolib-${geckolib}-beta.2-dev.jar")
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs)
    {
        exclude 'mcmod.info'
    }
}

/* Comparing keys */
task compareLangFiles {
    doLast {
        def help = file("src/main/resources/assets/${project.archivesBaseName}/lang/")
        def parse = { File file ->
            def map = new HashMap()
            def splits = file.text.split("\n");

            for (split in splits) {
                def index = split.indexOf("=")

                if (index == -1) {
                    continue
                }

                map.put(split.substring(0, index), true)
            }

            return map
        }

        if (!help.exists()) {
            return
        }

        def en = file("${help.getAbsolutePath()}/en_US.lang")
        def en_map = parse(en)

        for (lang_file in help.listFiles()) {
            if (!lang_file.name.endsWith(".lang") || lang_file.name.startsWith("en_US")) {
                continue
            }

            def lang_map = parse(lang_file)
            def missing = new ArrayList()
            def extra = new ArrayList()

            for (key in en_map.keySet()) {
                if (!lang_map.containsKey(key)) {
                    missing.add(key)
                }
            }

            for (key in lang_map.keySet()) {
                if (!en_map.containsKey(key)) {
                    extra.add(key)
                }
            }

            missing.sort()
            extra.sort()

            print("\n")

            if (!missing.isEmpty() || !extra.isEmpty()) {
                if (!missing.isEmpty()) {
                    print("Language file ${lang_file.getName()} misses following keys:\n")

                    for (key in missing) {
                        print("- ${key}\n")
                    }
                }

                if (!extra.isEmpty()) {
                    print("Language file ${lang_file.getName()} has additional keys:\n")

                    for (key in extra) {
                        print("- ${key}\n")
                    }
                }
            }
            else {
                print("Language file ${lang_file.getName()} fully matches ${en.getName()}!\n")
            }
        }
    }
}